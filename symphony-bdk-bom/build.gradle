plugins {
    id 'java-platform'
}

description = 'Symphony Java BDK Bom (Bill Of Materials)'

javaPlatform {
    allowDependencies()
}

repositories {
    mavenCentral()
}

dependencies {
    // import Spring Boot's BOM
    api platform('org.springframework.boot:spring-boot-dependencies:2.4.0')

    // define all our dependencies versions
    constraints {
        // Internal modules dependencies (Keep them first)
        api "com.symphony.platformsolutions:symphony-bdk-core:$project.version"
        api "com.symphony.platformsolutions:symphony-bdk-http-api:$project.version"
        api "com.symphony.platformsolutions:symphony-bdk-http-jersey2:$project.version"
        api "com.symphony.platformsolutions:symphony-bdk-http-webclient:$project.version"
        api "com.symphony.platformsolutions:symphony-bdk-core-spring-boot-starter:$project.version"
        api "com.symphony.platformsolutions:symphony-bdk-app-spring-boot-starter:$project.version"
        api "com.symphony.platformsolutions:symphony-bdk-template-api:$project.version"
        api "com.symphony.platformsolutions:symphony-bdk-template-freemarker:$project.version"
        api "com.symphony.platformsolutions:symphony-bdk-template-handlebars:$project.version"

        // External dependencies
        api 'org.projectlombok:lombok:1.18.12'

        api 'org.apiguardian:apiguardian-api:1.1.0'

        api 'org.slf4j:slf4j-api:1.7.30'
        api 'org.slf4j:slf4j-log4j12:1.7.30'

        api 'commons-io:commons-io:2.6'
        api 'commons-codec:commons-codec:1.15'
        api 'commons-beanutils:commons-beanutils:1.9.4'
        api 'org.apache.commons:commons-lang3:3.9'
        api 'com.brsanthu:migbase64:2.2'
        api 'io.jsonwebtoken:jjwt:0.9.1'
        api 'org.bouncycastle:bcpkix-jdk15on:1.66'
        api 'com.google.code.findbugs:jsr305:3.0.2'

        api 'io.github.resilience4j:resilience4j-retry:1.4.0'

        api 'com.fasterxml.jackson.core:jackson-databind:2.11.2'
        api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.2'
        api 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.2'

        api 'io.swagger:swagger-annotations:1.6.0'
        api 'org.openapitools:jackson-databind-nullable:0.2.1'

        api 'org.glassfish.jersey.core:jersey-client:2.32'
        api 'org.glassfish.jersey.inject:jersey-hk2:2.32'
        api 'org.glassfish.jersey.media:jersey-media-multipart:2.32'
        api 'org.glassfish.jersey.media:jersey-media-json-jackson:2.32'
        api 'org.glassfish.jersey.connectors:jersey-apache-connector:2.32'

        api 'org.projectreactor:reactor-spring:1.0.1.RELEASE'

        api 'org.freemarker:freemarker:2.3.30'
        api 'org.reflections:reflections:0.9.12'

        api 'org.junit.jupiter:junit-jupiter:5.7.0'
        api 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
        api 'ch.qos.logback:logback-classic:1.2.3'
        api 'com.tngtech.archunit:archunit-junit5:0.14.1'
        api 'org.mock-server:mockserver-netty:5.11.1'
        api 'org.mockito:mockito-core:3.4.6'
        api 'org.mockito:mockito-junit-jupiter:3.4.6'
    }
}

publishing {
    publications {
        bom(MavenPublication) {
            from components.javaPlatform
            pom(rootProject.ext.pomDefinition)
            pom {
                name = project.name
                description = project.description
            }
        }
    }
}

signing {
    required { rootProject.isReleaseVersion }
    sign publishing.publications.bom
}
